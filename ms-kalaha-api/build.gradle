plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id "org.sonarqube" version "3.0"
    id 'groovy'
    id 'jacoco'
}

group = 'com.bol.kalaha'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // kalaha game core
    implementation project(":kalaha-core")

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    // data
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // documentation
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-spring:2.2-M3-groovy-4.0'
    testImplementation 'io.github.benas:random-beans:3.9.0'
}

archivesBaseName = "${project.name}"

jar {
    enabled = true
    archiveFileName = "${project.name}-light.jar"
}

test {
    useJUnitPlatform()
}

def coverageExclusionList = [
        "**/com/bol/kalaha/api/model/**/*",
        "**/com/bol/kalaha/api/Application.java",
        "**/com/bol/kalaha/api/config/**/*",
]

sonarqube {
    properties {
        property 'sonar.coverage.exclusions', coverageExclusionList.join(", ")
    }
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test.finalizedBy jacocoTestReport